<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" width="900" height="640" showStatusBar="false"
					   xmlns:s="library://ns.adobe.com/flex/spark"  addedToStage="addedToStageHandler()"
					   xmlns:mx="library://ns.adobe.com/flex/mx" frameRate="30">
	<fx:Declarations>
		<s:RadioButtonGroup id="animWrap"/>
		<!-- 将非可视元素（例如服务、值对象）放在此处 -->
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import bing.res.ResVO;
			import bing.utils.ContainerUtil;
			
			import ghostcat.fileformat.swf.SWFDecoder;
			import ghostcat.fileformat.swf.tag.SymbolClassTag;
			
			import local.ele.BitmapMovieClip;
			import local.utils.ResourceUtil;
			
			import mx.collections.ArrayCollection;
			
			import spark.events.IndexChangeEvent;
			
			private var file:File ;
			private var swfDecoder:SWFDecoder ; 
			private var classListCollection:ArrayCollection ; //链接名列表
			private var actionListCollection:ArrayCollection ; //动作列表
			private var bmpMC:BitmapMovieClip ;
			
			private function addedToStageHandler():void{
				uiContainer.graphics.lineStyle(2);
				uiContainer.graphics.moveTo(-50,0);
				uiContainer.graphics.lineTo(50,0);
				uiContainer.graphics.moveTo(0,-25);
				uiContainer.graphics.lineTo(0,25);
				this.addEventListener(Event.ENTER_FRAME , updateHandler );
			}
			
			
			//---------------直接拖放------------------------------------
			protected function cavBuilding_nativeDragEnterHandler(event:NativeDragEvent):void
			{
				NativeDragManager.dropAction = NativeDragActions.COPY ; 
				NativeDragManager.acceptDragDrop(event.target as InteractiveObject) ;
			}
			
			protected function cavBuilding_nativeDragDropHandler(event:NativeDragEvent):void
			{
				dispose();
				file =  event.clipboard.getData( event.clipboard.formats[0])[0] as File;
				if(file.extension.toLowerCase()=="swf"){
					ResourceUtil.instance.addEventListener( file.name , loadedHandler );
					ResourceUtil.instance.loadRes( new ResVO(file.name,file.url) );
				}
			}
			
			private function loadedHandler( e:Event ):void
			{
				e.stopPropagation() ;
				var resVO:ResVO = ResourceUtil.instance.getResVOByResId( file.name );
				if(resVO){
					var bytes:ByteArray = resVO.userData as ByteArray ;
					bytes.position = 0 ;
					//破解swf
					swfDecoder = new SWFDecoder(bytes);
					var arr:Array = swfDecoder.getTags(SymbolClassTag)[0].symbolClasses ;
					classListCollection = new ArrayCollection( arr );
					classList.dataProvider = classListCollection ;
					
					classList.selectedIndex = 0 ;
					classList_changeHandler(null);
				}
			}
			
			protected function classList_changeHandler(event:IndexChangeEvent):void
			{
				dispose(false);
				
				if(classList.selectedIndex<0) return ;
				
				var role:MovieClip = ResourceUtil.instance.getInstanceByClassName( file.name,classListCollection[classList.selectedIndex]) as MovieClip;
				if(role){
					var actions:Array = [];
					for( var i:int = 0 ; i<role.currentLabels.length ; ++i){
						actions.push ( (role.currentLabels[i] as FrameLabel).name );
					}
					actionListCollection = new ArrayCollection(actions) ;
					actionList.dataProvider = actionListCollection ;
					bmpMC = new BitmapMovieClip( role );
					uiContainer.addChild( bmpMC );
					bmpMC.visible=false;
				}
			}
			
			private function updateHandler( e:Event ):void
			{
				if(bmpMC && bmpMC.update()){
					bmpMC.x = bmpMC.getBound().x ;
					bmpMC.y = bmpMC.getBound().y ;
					bmpMC.visible=true;
				}
			}
			
			protected function actionList_changeHandler(event:IndexChangeEvent):void
			{
				var currAction :String = actionListCollection[actionList.selectedIndex] ;
				if(bmpMC){
					bmpMC.gotoAndPlay( currAction );
				}
			}
			
			protected function zoom_changeHandler(event:Event):void
			{
				uiContainer.scaleX = uiContainer.scaleY = zoom.value ;
			}
			
			private function dispose( flag:Boolean= true ):void
			{
				if(bmpMC) {
					bmpMC.dispose(true);
					bmpMC = null ;
				}
				ContainerUtil.removeChildren( uiContainer );
				if(file && flag){
					ResourceUtil.instance.deleteRes( file.name );
				}
			}
		]]>
	</fx:Script>
	
	<mx:TabNavigator x="0" y="0" width="100%" height="100%">
		<s:NavigatorContent width="100%" height="100%" label="人物及特效查看">
			<s:HGroup x="0" y="0" width="100%" height="100%">
				<s:BorderContainer width="80%" height="100%" backgroundColor="#E0E8E0" mouseChildren="false"
								   nativeDragEnter="cavBuilding_nativeDragEnterHandler(event)" nativeDragDrop="cavBuilding_nativeDragDropHandler(event)">
					<mx:UIComponent id="uiContainer" horizontalCenter="0" verticalCenter="0"/>
				</s:BorderContainer>
				<s:VGroup width="20%" height="100%" horizontalAlign="center">
					<s:Label text="链接名列表"/>
					<s:List id="classList" width="100%" height="142" change="classList_changeHandler(event)"></s:List>
					<s:Spacer height="20" width="100%"/>
					<s:Label text="动作列表"/>
					<s:List id="actionList" enabled="false" width="100%" height="250" change="actionList_changeHandler(event)"></s:List>
					<s:Spacer height="20" width="100%"/>
					<s:Label text="缩放大小"/>
					<s:HSlider id="zoom" width="80%" change="zoom_changeHandler(event)" maximum="1"
							   stepSize="0.1" value="1"/>
				</s:VGroup>
			</s:HGroup>
		</s:NavigatorContent>
	</mx:TabNavigator>
</s:WindowedApplication>
