<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009"
					   xmlns:s="library://ns.adobe.com/flex/spark"
					   xmlns:mx="library://ns.adobe.com/flex/mx"  xmlns:ns1="*" 
					   width="900" height="700" backgroundColor="#0C0C0C" creationComplete="init();" >
	<fx:Declarations>
		<!-- 将非可视元素（例如服务、值对象）放在此处 -->
	</fx:Declarations>
	<fx:Style source="MainStyle.css" />
	<fx:Script>
		<![CDATA[
			import bing.iso.IsoGrid;
			import bing.iso.Rhombus;
			import bing.utils.ContainerUtil;
			
			import comm.GameData;
			import comm.GameSetting;
			
			import enums.BuildingCurrentOperation;
			
			import map.GameWorld;
			
			import mx.controls.Alert;
			import mx.graphics.codec.PNGEncoder;
			
			import utils.ReadMapDataUtil;
			import utils.SaveMapDataUtil;
			
			private var _gameWorld:GameWorld;
			
			private function init():void
			{
				this.maximize();
				borderContainer.contentGroup.clipAndEnableScrolling = true ;
				_gameWorld  = new GameWorld() ;
				container.addChild( _gameWorld) ;
				
				GameData.mainApp = this ;
			}
			
			private function addImpactClick():void
			{
				ContainerUtil.removeChildren(_gameWorld.mouseContainer);
				_gameWorld.mouseContainer.addChild( new Rhombus(GameSetting.GRID_SIZE,GameSetting.RoadColor));
				GameData.buildingCurrOperation = BuildingCurrentOperation.ADD_IMPACT ;
			}
			
			private function addForbiddenClick():void
			{
				ContainerUtil.removeChildren(_gameWorld.mouseContainer);
				_gameWorld.mouseContainer.addChild( new Rhombus(GameSetting.GRID_SIZE,GameData.currentMapColor));
				GameData.buildingCurrOperation = BuildingCurrentOperation.ADD_FORBIDDEN ;
			}
			
			private function saveClick():void
			{
				(new SaveMapDataUtil() ).save() ;
			}
			
			private function loadClick():void
			{
				( new ReadMapDataUtil()).read();
			}
						
			private function hideImpactScene():void
			{
				GameWorld.instance.impactScene.visible = !GameWorld.instance.impactScene.visible;
			}
			
			private function hideForbiddenScene():void
			{
				GameWorld.instance.forbiddenScene.visible = !GameWorld.instance.forbiddenScene.visible;
			}
			
			
			
			//=====================保存网格==============
			private var _file:File ;
			private function saveGrid():void
			{
				_file = new File();
				_file.addEventListener(Event.SELECT , selectedFileHandler );
				_file.browseForDirectory("选择保存位置");
			}
			
			private function selectedFileHandler( e:Event ):void
			{
				var grid:IsoGrid = new IsoGrid(GameSetting.GRID_X,GameSetting.GRID_Z,GameSetting.GRID_SIZE);
				grid.render();
				var bmd:BitmapData = new BitmapData( grid.width/2,grid.height/2,false,0);
				var mat:Matrix = new Matrix();
				mat.translate(grid.width/2,0);
				mat.scale(.5,.5);
				bmd.draw( grid,mat );
				var encoder:PNGEncoder = new PNGEncoder();
				var bytes:ByteArray = encoder.encode( bmd );
				var stream:FileStream = new FileStream();
				try{
					var file:File = new File(_file.url+"/grid.png") ;
					stream.open( file , FileMode.WRITE );
					stream.writeBytes( bytes, 0 ,bytes.bytesAvailable);
					Alert.show("保存成功");
				}catch( e:Error ){
					trace(e);
				}finally{
					stream.close();
				}
			}
			
			
			
			//=============加密swf资源===============================
			private function encryptionSWF():void
			{
				_file = new File();
				_file.addEventListener(Event.SELECT , selectedDirHandler );
				_file.browseForDirectory("选择文件夹");
			}
			private function selectedDirHandler(e:Event):void
			{
				var bakFile:File = new File(_file.url+"/bak");
				if(!bakFile.exists) bakFile.createDirectory();
				
				var files:Array = _file.getDirectoryListing();
				if(files){
					var len:int = files.length ;
					var file:File ;
					var stream:FileStream ;
					var bytes:ByteArray ;
					var newFile:File ;
					for( var i:int = 0 ; i<len; ++i)
					{
						file = files[i] ;
						if(!file.isDirectory && file.extension.toLocaleLowerCase()=="swf")
						{
							file.copyTo( new File(bakFile.url+"/"+file.name) , true );
							stream = new FileStream();
							stream.open( file,FileMode.READ); ;
							bytes = new ByteArray();
							stream.readBytes( bytes);
							if(bytes.readByte()==1) continue ;
							else bytes.position=0;
							stream.close();
							
							newFile = new File( file.nativePath);
							stream = new FileStream();
							stream.open( newFile , FileMode.WRITE);
							stream.writeByte(1);
							stream.writeBytes( bytes );
							stream.close();
						}
					}
					Alert.show("加密完成");
				}
			}
		]]>
	</fx:Script>
	<s:VGroup x="0" y="0" width="100%" height="100%">
		<s:BorderContainer width="100%" height="50">
			<s:layout>
				<s:HorizontalLayout gap="10" verticalAlign="middle"/>
			</s:layout>
			<s:Button id="createMap" height="100%" label="创建地图" />
			<s:Button id="loadMap" height="100%" label="加载地图" click="loadClick();" />
			<s:Button id="saveMap" height="100%" label="保存地图"  click="saveClick();"/>
			<mx:VRule height="80%" />
			<s:Button id="addImpact" height="100%" label="寻路数据" toolTip="设置角色除了地图区域外还可以走的地方" click="addImpactClick()"/>
			<s:Button id="addForbidden" height="100%" label="地图区域" toolTip="设置三个地图的建筑区域"  click="addForbiddenClick();"/>
			<ns1:MapColorComponent width="100%">
			</ns1:MapColorComponent>
			<mx:VRule height="80%" />
			<s:Button height="100%" label="保存网格" click="saveGrid();"/>
			<s:Button id="hideImpact" height="100%" label="隐藏和显示碰撞层" click="hideImpactScene();"/>
			<s:Button id="hideForbidden" height="100%" label="隐藏和显示建筑禁止区域" click="hideForbiddenScene();"/>
			<s:Button id="encryption" height="100%" label="加密swf资源" click="encryptionSWF();"/>
		</s:BorderContainer>
		<s:BorderContainer id="borderContainer" width="100%" height="100%">
			<mx:UIComponent id="container" width="100%" height="100%"/>
		</s:BorderContainer>
	</s:VGroup>
</s:WindowedApplication>
